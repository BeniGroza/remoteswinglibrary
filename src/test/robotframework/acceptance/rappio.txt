*** Settings ***
Library    Rappio
Library    OperatingSystem
Library    Process
Suite setup    Set Environment Variable      CLASSPATH     target/test-classes

*** Test Cases ***
None Existing Application start fails before timeout
     [Timeout]    5 seconds
     Run Keyword And Expect Error    *    Start Application    no-one    this command fails immediatly   timeout=1

Starting a java application without main window fails
     Run keyword and expect error   *Agent port not received before timeout   Start Application    myapp    java org.robotframework.rappio.MyApp  timeout=5

Starting and stopping application with main window
     Start Application    myapp2    java org.robotframework.rappio.MySwingApp  timeout=5
     System Exit          myapp2

Connecting to a started application
     ${handle}=  Start Process   java org.robotframework.rappio.MySwingApp   shell=True
     Application Started     myjava
     Exit and check process   ${handle}    myjava

Connecting to a specific application
     ${handle1}=  Start Process   java org.robotframework.rappio.MySwingApp one    shell=True
     ${handle2}=  Start Process   java org.robotframework.rappio.MySwingApp two    shell=True
     ${handle3}=  Start Process   java org.robotframework.rappio.MySwingApp three  shell=True
     Application Started    three    name=*three*
     Application Started    one      name=*one*
     Application Started    two      name=*two*
     Exit and check process    ${handle2}   two
     Exit and check process    ${handle3}   three
     Exit and check process    ${handle1}   one

*** Keywords ***
Exit and check process
   [Arguments]    ${handler}   ${alias}
   Process Should Be Running    ${handler}
   System Exit   ${alias}
   Process Should Be Stopped    ${handler}